*vimdo.txt*    vimdo     Version 0.1.0    Last change: October 11 2018


DO.VIM - TABLE OF CONTENTS                                    *do.vim* *vimdo*
==============================================================================

  Introduction                                        |vimdo-introduction|
  Quick start                                         |vimdo-quick-start|
  As inputlist() replacement                          |vimdo-inputlist|
  Groups                                              |vimdo-groups|
  Options                                             |vimdo-options|
  Interactive mode                                    |vimdo-interactive-mode|
  Group dictionary                                    |vimdo-group-dictionary|
  Arbitrary groups                                    |vimdo-arbitrary-groups|
  Default commands                                    |vimdo-default-commands|




INTRODUCTION                                              *vimdo-introduction*
==============================================================================

The concept is to have a group of commands that `do something`, in a way that
it makes it easy to remember their mappings, and that you can show by running
a command.

There is a default prefix (`do`) with a set of commands that you can activate,
but it will work with any of your mappings.

What's the difference between this and just typing `:nmap ...`?

- you can set descriptions for each mapping, and the output is more readable.
- you can filter entries by pressing a key.
- it can be run in interactive mode.




QUICK START                                                *vimdo-quick-start*
==============================================================================
                                                                    *:ShowDos*
Use the main command to show all mappings that start with a prefix:
>
  :ShowDos g
<
you don't need any customization for this. But if you want to add some
description to your custom mappings?

Say you have a bunch of mappngs that start with `g`, and you want to add some
description, so that it will show up when you run the command above.
You create a group for `g`, and you write the descriptions inside:
>
  let g:vimdo = {}
  let g:vimdo.g = {}
  let g:vimdo.g.cc = 'comment line toggle'
  let g:vimdo.g.ot = 'open terminal at current file position'

Where the keys inside the `g` group are the mappings `gcc`, `got`, etc.

If some of these mappings are <buffer> mappings (for example you defined them
in a |ftplugin|), you can call the command with a <bang>, and it will only
show <buffer> mappings:
>
    :ShowDos! g

That's it for a basic usage.
                                                                       *:Nmap*
A little extra: the command `Nmap` is defined (if not already). It prints
mappings as `:verbose nmap` would, but with a more compact formatting.
Just as `ShowDos!`, `Nmap!` prints |<buffer>| mappings only.

Finally, if you don't like the commands' names, you can redefine them.
See |g:vimdo_showdos_command| and |g:vimdo_nmap_command|.





AS INPUTLIST() REPLACEMENT                                   *vimdo-inputlist*
==============================================================================

It's not meant to be run with the |:ShowDos| command, but rather invoked
through a function call, that will return the typed character.

It accepts a single character, so it's not meant for complex choices either.
Think of it as an alternative to |inputlist()| that needs no <Return>.


Example:
>
  " with pairs choice/description
  let result = do#menu({ 'label': 'Type a letter: ',
      \'a':  'run option "a"',
      \'b':  'run option "b"',
      \'c':  'run option "c"',
      \})

  " or with a list of choices
  let result = do#menu({'label': 'Type a letter: ', 'items':  ['a', 'b', 'c']})

  " or as a pure list, using g:vimdo_default_menu_label
  let result = do#menu(['a', 'b', 'c'])

  if result == 'a'
    echo 'you chose "a"'
  else
    echo 'you chose something else'
  endif




GROUPS                                                          *vimdo-groups*
==============================================================================

An example of creating a new group with `do` prefix.

First, you assign your mappings, as you normally would. Examples:

Copy entire buffer to system clipboard: >
  nnoremap doca   :%y+<cr>

Select entire buffer: >
  nnoremap dosa   ggVG

Json pretty print: >
  nnoremap dojpp  :%!python -m json.tool<cr>

Set very large tabs (table mode): >
  nnoremap dotm   :setlocal noexpandtab tabstop=20 shiftwidth=20 softtabstop=20 nowrap<cr>

Trim whitespaces (and print their number): >
  nnoremap dows   :m`:keeppatterns %s/\s\+$//gn<CR>:keeppatterns silent! %s/\s\+$//e<CR>``

You initialize the `g:vimdo` dict (if you didn't already!):
>
  let g:vimdo = {}

Then register descriptions for the mappings (see that the mapping prefix isn't
repeated):
>
  let g:vimdo.do = {
    \'ca':  'copy all',
    \'sa':  'select all',
    \'jpp': 'json pretty print',
    \'tm':  'table mode',
    \'ws':  'trim whitespaces',
    \}

Finally you create a mapping to show your group:
>
  nnoremap dO  :ShowDos do<cr>

------------------------------------------------------------------------------

An example of creating a new group with fugitive.vim commands.

You assign your mappings:
>
  nnoremap \gs  :Gstatus<cr>
  nnoremap \gp  :Gpush<cr>
  nnoremap \gl  :0Glog<cr>
  nnoremap \gd  :Gvdiff<Space>
  nnoremap \gc  :Gcommit<cr>
  nnoremap \ga  :Gcommit --amend<cr>

You initialize the new group:
>
  let g:vimdo['\g'] = { 'label' : 'git commands',
    \'s':  'status',
    \'p':  'push',
    \'l':  'log for current file',
    \'d':  'diff',
    \'c':  'commit',
    \'a':  'amend commit',
    \}

Inside the group dictionary you can see:
- group options (see |vimdo-group|)
- pairs (continuation of) mapping/description

Finally you create a mapping to show your group:
>
  nnoremap \g? :ShowDos \g<cr>

------------------------------------------------------------------------------

|Important|: if you create groups for special keys, they should be
capitalized when you define the group:
>
  let g:vimdo['<Space>'] = { 'label' : '<Space> mappings', ... }

But then it doesn't matter how you run the command:
>
  :ShowDos <space>



OPTIONS                                                        *vimdo-options*
==============================================================================
                                                      *g:vimdo_print_full_lhs*
  Default: 1~

    To print the full {lhs}, inclusive of the prefix.

                                                            *g:vimdo_show_rhs*
  Default: 1~

    Show the {rhs} of each mapping.

                                                       *g:vimdo_show_filename*
  Default: 0~

    To show the file where the mapping is defined.

                                                              *g:vimdo_simple*
  Default: 0~

    Just show the mappings.

                                                             *g:vimdo_compact*
  Default: 0~

    Use the compact view by default. Not recommended since compact mode
    requires descriptions, see |vimdo-require_description|.

                                                         *g:vimdo_interactive*
  Default: 0~

    To run in interactive mode by default (see |vimdo-interactive|).

                                                *g:vimdo_use_default_commands*
  Default: 0~

    Create mappings for the default commands.

                                                  *g:vimdo_default_menu_label*
  Default: 'Choose: '~

    Default label for menus created with |vimdo-inputlist|.

                                                      *g:vimdo_default_prefix*
  Default: 'do'~

    If you want to change the prefix for the |vimdo-default-commands|.

                            *g:vimdo_showdos_command*   *g:vimdo_nmap_command*

    You can change the default name for the `ShowDos` and `Nmap` commands:
>
      let g:vimdo_showdos_command = 'ShowMaps'
      let g:vimdo_nmap_command = 'Mappings'
<


INTERACTIVE MODE                                           *vimdo-interactive*
==============================================================================

The command can be run in interactive mode if the global option is set, or if
a custom group contains the 'interactive' dictionary key.

In this case, the pressed keys will not only refilter the list, but also
trigger the mapping if a match in the list is found.




GROUP DICTIONARY                                                 *vimdo-group*
==============================================================================

In the created dictionary, there can be these optional keys:

- `label`
- `simple`
- `interactive`
- `require_description`
- `arbitrary`
                                                                 *vimdo-label*
`label`: just a label for the header.
                                                                *vimdo-simple*
`simple`: no separators, no 'Press key...' message.

`interactive`: run in interactive mode.
                                                   *vimdo-require_description*
`require_description`: Setting this option prevents unwanted mappings to be
    shown in the list, but forces you to add dictionary entries for all items.

                                                             *vimdo-arbitrary*
`arbitrary`: if true, the group name is not the prefix, it's just a group
    name. The whole keys will be checked for a mapping, but only those ones.
    This allows you to show a set of mapping of your choosing.
    See |vimdo-arbitrary-groups|.

------------------------------------------------------------------------------

These variables control the padding of mappings/descriptions:
>
  let g:vimdo_keys_width = 16
  let g:vimdo_desc_width = 40

------------------------------------------------------------------------------

More optional group dictionary keys:

- `compact`
- `order`
- `keys_width`
- `desc_width`
- `show_rhs`
                                                               *vimdo-compact*
`compact`: lets you show a group in a compact way, on multiple columns, without
    showing the {rhs} of the mapping, only the trigger and the description.
    Note: if `compact` is set, `require_description` is also considered true.

                                                                 *vimdo-order*
`order`: since dictionaries don't preserve order, this key can hold the order
    in which you want the group to be shown. All group mappings must be
    present in this list, or it will fail.

                                                                *vimdo-widths*
`keys_width` and `desc_width`: especially useful to set for a group, when
    using compact mode.

                                                              *vimdo-show_rhs*
`show_rhs`: if present and set to 0, will prevent the rhs of the mappings from
    being displayed.





ARBITRARY GROUPS                                      *vimdo-arbitrary-groups*
==============================================================================

This kind of menu requires `arbitrary` to be set in the group.

You can have groups with elements that aren't simple descriptions, but that
include the full commands, and don't require a mapping. You can then create
contextual menus that execute custom commands at keypress.

The dictionary element must be a list and not a string:
>
    {key} : [ 'description', "rhs" ]

If the `rhs` is recursive, add a third element:
>
    {key} : [ 'description', "rhs", 1 ]

Note: the {rhs} of the custom command is run with |feedkeys()|, so it must use
`double quotes` and special characters must be escaped.



DEFAULT COMMANDS                                      *vimdo-default-commands*
==============================================================================

If you activated the default commands and didn't change the default prefix,
these mappings are set:
>
    dO    ( show all do's )
    doB   ( show all buffer do's )

If NOT using the default prefix, these mappings will be:
>
    {prefix}?   ( show all do's )
    {prefix}!   ( show all buffer do's )

These are the default commands:

`doft`  ->  do open ftplugin file (custom preferred)
`dofT`  ->  do open ftplugin file (stock vim/nvim only)
`dosn`  ->  do open snippets file for the current filetype
`dorf`  ->  do reindent file
`docf`  ->  do copy file (backup)
`dodo`  ->  do diff other file (in next window)
`dods`  ->  do diff with saved
`dout`  ->  do update tags (runs `ctags -R .`, or `b:ctags_cmd` if defined)
`dovp`  ->  do vim profiling (first time activates, second time stops and quits)
`doss`  ->  do print syntax infos for word under cursor
`doec`  ->  do echo color infos
`dofcr` ->  do find CR (find/correct files with CRLF line endings)

If you don't want all defaults, but want some of them, add them to your `vimrc`:
>
  nnoremap doft  :call do#cmd#open_ftplugin()<cr>
  nnoremap dofT  :call do#cmd#open_ftplugin(1)<cr>
  nnoremap dosn  :call do#cmd#snippets()<cr>
  nnoremap dorf  :call do#cmd#reindent_file()<cr>
  nnoremap docf  :call do#cmd#copy_file()<cr>
  nnoremap dodo  :call do#diff#other()<cr>
  nnoremap dods  :call do#diff#saved()<cr>
  nnoremap dout  :call do#cmd#update_tags()<cr>
  nnoremap dovp  :call do#cmd#profiling()<cr>
  nnoremap dofcr :call do#cmd#find_crlf(1, "")<cr>
  nnoremap doss  :call do#cmd#syntax_attr()<cr>
  nnoremap doec  :call do#color#echo()<cr>




==============================================================================
vim:tw=78:et:sw=2:ts=2:ft=help:norl:nowrap:
